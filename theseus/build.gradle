// -----
// PLUGINS
// -----

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.6'
  }
}

apply plugin: 'java'
apply plugin: 'com.google.protobuf'


// -----
// BASICS
// -----

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"


// -----
// PROTOBUF
// -----

// Configure Protocol Buffers + gRPC
protobuf {
  generatedFilesBaseDir = "$projectDir/src/gen/proto/java"
  protoc {
    // The version of protoc must match protobuf-java. If you don't depend on
    // protobuf-java directly, you will be transitively depending on the
    // protobuf-java version that grpc depends on.
    artifact = "com.google.protobuf:protoc:3.5.1"
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.9.0'
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}


// -----
// SOURCES
// -----

sourceSets {
  main {
    java {
      srcDir 'src/main/java'
      srcDir 'src/gen/proto/java/main/grpc'
      srcDir 'src/gen/proto/java/main/java'
    }
    resources {
      srcDir 'src/main/resources'
    }
  }
  test {
    java {
      srcDir 'src/test/java'
    }
    resources {
      srcDir 'src/test/resources'
    }
  }
}


// -----
// TASKS
// -----

test {
  useJUnit {
    excludeCategories 'ai.eloquent.test.IntegrationTests'
    excludeCategories 'ai.eloquent.test.SlowTests'
  }
}

task itest(type: Test) {
  useJUnit {
    includeCategories 'ai.eloquent.test.IntegrationTests'
  }
}

task slowtest(type: Test) {
  useJUnit {
    includeCategories 'ai.eloquent.test.SlowTests'
  }
}


task dist(type: Copy) {
  dependsOn compileJava
  from configurations.runtime
  from jar
  into "${project.projectDir}/build/dist"
}


// -----
// DEPENDENCIES
// -----

repositories {
  mavenCentral()
}

dependencies {
  compile project(':public_common')

  // gRPC
  compile (group: 'io.grpc', name: 'grpc-core',     version: '1.15.0')
  compile (group: 'io.grpc', name: 'grpc-stub',     version: '1.15.0')
  compile (group: 'io.grpc', name: 'grpc-protobuf', version: '1.15.0')
  compile (group: 'io.grpc', name: 'grpc-netty',    version: '1.15.0')


  //
  // TEST DEPENDENCIES
  //

  // JUnit (for testing)
  testCompile group: 'junit', name: 'junit', version: '4.12'
  // Google guava for some utils
  testCompile 'com.google.guava:guava:26.0-jre'
  // SLF4J Simple so we can actually log things
  testCompile group: 'org.slf4j', name: 'slf4j-simple',      version: '1.7.13'
  // Java Microbenchmarking Harness
  testCompile group: 'org.openjdk.jmh', name: 'jmh-core', version: '1.21'
  testCompile group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: '1.21'


}
  
task writeTheseusClasspath {
  doLast {
    buildDir.mkdirs()
    new File(buildDir, "classpath.txt").text = configurations.runtime.asPath
  }
}
